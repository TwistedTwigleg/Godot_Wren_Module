#!/usr/bin/env python
Import("env")
Import("env_modules")

env_wren = env_modules.Clone()

# Add the Wren source files
# ===============

# Thirdparty source files
thirdparty_obj = []

thirdparty_dir = "#modules/wren/src/"
thirdparty_sources = [
    "vm/wren_compiler.c",
    "vm/wren_core.c",
    "vm/wren_debug.c",
    "vm/wren_primitive.c",
    "vm/wren_utils.c",
    "vm/wren_value.c",
    "vm/wren_vm.c",
    "optional/wren_opt_meta.c",
    "optional/wren_opt_random.c",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_wren.Prepend(CPPPATH=[thirdparty_dir])

env_wren.Prepend(CPPPATH=["#modules/wren/src/include/"])
env_wren.Prepend(CPPPATH=["#modules/wren/src/vm/"])
env_wren.Prepend(CPPPATH=["#modules/wren/src/optional/"])

env_thirdparty = env_wren.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
env.modules_sources += thirdparty_obj

# ===============


# Add the Godot specific files
# ===============
module_obj = []
env_wren.add_source_files(module_obj, "*.cpp")
env_wren.add_source_files(module_obj, "foreign_classes/*.cpp")
env_wren.add_source_files(module_obj, "wrapper_classes/*.cpp")
env.modules_sources += module_obj

env.Depends(module_obj, thirdparty_obj)
# ===============
